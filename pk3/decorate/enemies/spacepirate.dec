ACTOR SpacePirate 17010
{
    Health 150
    PainChance 80
    Scale 0.75
    Speed 12
    Radius 24
    Height 56
    Gravity 0.5
    Mass 400
    Monster
    +FLOORCLIP
    +MISSILEMORE
    SeeSound "demon/sight"
    PainSound "demon/pain"
    DeathSound "demon/death"
    ActiveSound "demon/active"
    DamageFactor "NoDamage", 0.0
    Obituary "%o was shot down by a Space Pirate."
    States
    {
    Spawn:
        PIRT A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_ChangeFlag("PAINLESS",0)
        PIRT AABBCCDD 2 A_Chase
        Loop
    Missile:
        TNT1 A 0 A_Jump(32,"Missile3")
        TNT1 A 0 A_Jump(64,"Missile2")
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        PIRT ABCD 2 A_Wander
        TNT1 A 0 A_Jump(128,"See")
        TNT1 A 0 A_CPosRefire
        goto Missile
    Missile2:
        TNT1 A 0 A_Jump(256,"StrafeLeft","StrafeRight")
    StrafeLeft:
        TNT1 A 0 ThrustThingZ(0,random(10,20),0,1)
	    TNT1 A 0 ThrustThing(angle*256/360+192, random(16,24), 0, 0)
	    goto Missile2B
    StrafeRight:
        TNT1 A 0 ThrustThingZ(0,random(10,20),0,1)
	    TNT1 A 0 ThrustThing(angle*256/360+64, random(16,24), 0, 0)
    Missile2B:
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        goto See
    Missile3:
        TNT1 A 0 ThrustThingZ(0,random(40,60),0,1)
        TNT1 A 0 A_Recoil(random(-5,-10))
        TNT1 A 0 A_ChangeFlag("PAINLESS",1)
    Missile3B:
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        TNT1 A 0 A_CheckFloor("See")
        goto Missile3B
    Pain:
        TNT1 A 0
        PIRT A 2 A_Pain
        Goto See
    Death:
        PIRT G 8
        PIRT H 8 A_Scream
        PIRT I 4
        PIRT I 4 A_NoBlocking
        ACLO E -1
        Stop
    Death.IceBeam:
    Death.IceCharged:
    Death.IceBeamExplode:
    Death.IceChargedExplode:
        PIRT G 1
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("misc/freeze")
        TNT1 A 0 A_PlaySoundEx("ibeam/freeze","soundslot6")
        TNT1 A 0 A_SpawnItemEx("PirateguyFrozen",0,0,0,0,0,0,0,32)
        Stop
    }
}

actor PirateguyFrozen : FrozenDummy
{
    Scale 0.75
    Translation "Ice"
    States
    {
    Spawn:
        PIRT G 350
        PIRT G 1 A_Jump(128, "Death")
        Loop
    }
}

ACTOR PirateBolt
{
    Radius 6
    Height 8
    Speed 20
    Scale 0.35
    Damage (10)
    Projectile
    +RANDOMIZE
    RenderStyle Add
    Alpha 0.95
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        PSHT AABBCCDDEEFFGGHHIIJJKKLL 1 Bright A_SpawnItemEx("PirateBoltTrail",0,0,0,0,0,0,0,160)
        Loop
    Death:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("PirateBoltPuff",0,0,0,0,0,0,0,160)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PirateBoltPuff2",0,0,0,random(-300,300)/100.00,random(-300,300)/100.00,random(-300,300)/100.00,random(-180,180),160)
        Stop
    }
}

ACTOR PirateBoltPuff
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    Scale 0.5
    Renderstyle Add
    Alpha 0.75
    States
    {
    Spawn:
        TNT1 A 0
        PHIT ABCDEFGHIJKLMNOP 1 Bright A_FadeOut(0.025)
        stop
    }
}

ACTOR PirateBoltTrail
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    Scale 0.3
    Renderstyle Add
    Alpha 0.75
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(594,0,0,0) == 1,"Nope")
        PSHT ABCDEFGHIJKL 1 Bright A_FadeOut(0.05)
        stop
    Nope:
        TNT1 A 1
        stop
    }
}

ACTOR PirateBoltPuff2 : PirateBoltTrail
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    Scale 0.1
    Renderstyle Add
    Alpha 0.7
}