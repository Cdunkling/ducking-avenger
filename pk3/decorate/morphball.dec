actor MorphBallPlayer : PlayerPawn
{
    Radius 8
    Height 12
    Gravity 0.5
    Scale 0.75
    Player.JumpZ 0
    MaxStepHeight 12
    Player.Viewheight 21
    Player.ForwardMove 1.2, 1.2
    Player.SideMove 0.1, 0.2
    DamageFactor "NoDamage", 0.0
    Player.StartItem "BombCount", 3
    Player.SoundClass "Hunter"
    Player.DisplayName "Morph Ball"
    Player.MorphWeapon "Bombs"
    -PICKUP
    +NOSKIN
    +DOOMBOUNCE
    States
    {
      Spawn:
        PLAY X -1
        Stop

      See:
        PLAY X 3
        Loop

      Melee:
      Missile:
        PLAY X 5 BRIGHT
        Goto Spawn

      Pain:
        PLAY X 4 A_Pain
        Goto Spawn

    Death:
        TNT1 A 0
        PLAY H 10
        PLAY I 10 A_PlayerScream
        PLAY J 10 A_NoBlocking
        PLAY KLM 10
        PLAY N -1
        Stop

    XDeath:
        TNT1 A 0
        PLAY O 5
        PLAY P 5 A_XScream
        PLAY Q 5 A_NoBlocking
        PLAY RSTUV 5
        PLAY W -1
        Stop
    }
}

ACTOR BombCount : Counter { Inventory.MaxAmount 3 }

ACTOR "Bombs" : Weapon
{
    Weapon.SelectionOrder 3000
	Inventory.PickupMessage "Got the Morph Bombs!"
	Weapon.SlotNumber 1
	Obituary "%k blew %o up."
	+AMMO_OPTIONAL
    +NOAUTOFIRE
    +NOALERT
	States
	{
    Spawn:
	    MGUN A -1
        Stop
	Ready:
		TNT1 A 1 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("PowerBeamCharged",1,"FireCharged")
		Loop
	Select:
		TNT1 A 1 A_Raise
		TNT1 AA 0 A_Raise
		Loop
	Deselect:
		TNT1 A 1 A_Lower
		TNT1 AA 0 A_Lower
		Loop
	Fire:
        TNT1 A 0 A_JumpIfInventory("BombCount",1,1)
        goto Ready
        TNT1 A 0
		TNT1 A 0 A_FireCustomMissile("MorphBallBomb",0,0,0,0,0,0)
        TNT1 A 0 A_PlayWeaponSound("morphball/bombdrop")
		TNT1 A 4 A_TakeInventory("BombCount",1)
		Goto Ready
	FireCharged:
        TNT1 A 0
		TNT1 AAAAAA 0 A_FireCustomMissile("MorphBallBombCharged",random(-180,180),0,0,0,0,random(0,135))
        TNT1 A 0 A_StopSound(7)
        TNT1 A 0 A_StopSoundEx("soundslot7")
        //TNT1 A 0 A_PlayWeaponSound("morphball/bombdrop")
		TNT1 A 0 A_TakeInventory("PowerBeamCharged",1)
		Goto Ready
	}
}

ACTOR MorphBallBomb
{
    Radius 3
    Height 3
    Speed 0
    +NOGRAVITY
    -SOLID
    +NOCLIP
    +NOBLOOD
    //DeathSound "morphball/bombboom"
    States
    {
    Spawn:
        MBOM AABBCCDDAABBCCD 2
        TNT1 A 0 A_PlaySound("morphball/bombboom")
        TNT1 A 0 A_Explode(96,96,0)
        TNT1 A 0 A_GiveToTarget("BombCount",1)
        TNT1 A 0 A_SpawnItemEx("DummyExplode",0,0,-32,0,0,0,0,32)
        MBOM EFGHI 3
        stop
    }
}

ACTOR MorphBallBombCharged : MorphBallBomb
{
    -NOGRAVITY
    -NOCLIP
    -SOLID
    //+DOOMBOUNCE
    Speed 7
    Damage (15)
    States
    {
    Spawn:
        MBOM ABCDABCD 4
        TNT1 A 0 A_PlaySound("morphball/bombboom")
        TNT1 A 0 A_Explode(96,128,0)
        TNT1 A 0 A_GiveToTarget("BombCount",1)
        TNT1 A 0 A_SpawnItemEx("DummyExplodeCharged",0,0,-32,0,0,0,0,32)
        MBOM EFGHI 3
        stop
    }
}

ACTOR DummyExplode
{
    Radius 1
    Height 1
	Speed 0
	DamageType "NoDamage"
    +NOCLIP
    -SOLID
    +NOINTERACTION
    +NOBLOOD
    +PAINLESS
    Obituary "%k shouldn't have killed %o with this."
    States
    {
      Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode(128,96)
        Stop
    }
}

ACTOR DummyExplodeCharged : DummyExplode
{
    States
    {
      Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode(64,64)
        Stop
    }
}