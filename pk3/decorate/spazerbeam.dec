ACTOR "Spazer Beam" : Weapon replaces SuperShotgun
{
    Weapon.SelectionOrder 2000
	Inventory.PickupMessage "Got the Spazer Beam!"
	Obituary "%k spayed %o."
    Weapon.AmmoType ""
    Weapon.AmmoUse 0
    Weapon.AmmoType2 "MissileAmmo"
    Weapon.SlotNumber 2
    Weapon.AmmoUse2 1
	+AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL
    +NOAUTOFIRE
    +NOALERT
	States
	{
    Spawn:
	    SOUL B -1
        Stop
	Ready:
		SBEM A 1 A_WeaponReady
		Loop
	Select:
		SBEM A 1 A_Raise
		SBEM AA 0 A_Raise
		Loop
	Deselect:
		SBEM A 1 A_Lower
		SBEM AA 0 A_Lower
		Loop
	Fire:
		TNT1 A 0 A_PlayWeaponSound("sbeam/fire")
	    TNT1 A 0 A_Recoil(0.05)
		TNT1 A 0 A_FireCustomMissile("SpazerBolt1",-1,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBolt2",0,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBolt3",1,0,12,-2,0,0)
		TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_Light2
		SBEM B 1 BRIGHT A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
        TNT1 A 0 A_Light1
		SBEM C 1 BRIGHT A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		SBEM D 1 A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
		SBEM EEFFFEE 1 A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
        SBEM GHA 1 A_WeaponReady(WRF_NOSWITCH)
        SBEM A 6 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_Refire
		Goto Ready
    Hold:
        TNT1 A 0 A_JumpIfInventory("PowerBeamCharged",1,"HoldFinal2")
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",31,"HoldFinal")
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",26,"Hold7")
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",21,"Hold6")
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",16,"Hold5")
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",11,"Hold4")
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",6,"Hold3")
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",1,"Hold2")
        TNT1 A 0 A_PlaySoundEx("pbeam/charge","soundslot5")
    Hold2:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",31,"HoldFinal")
        TNT1 A 0 A_GiveInventory("PowerBeamChargeLevel",1)
		SBEM AA 1 BRIGHT A_SpawnItemEx("SpazerBeamCharging1",-15,64,28,0,0,0,-270,32) 
        TNT1 A 0 A_Refire
    HoldFire:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("silence","soundslot5")
        TNT1 A 0 A_TakeInventory("PowerBeamChargeLevel",34)
        TNT1 A 0 A_ClearRefire
		TNT1 A 0 A_PlayWeaponSound("sbeam/fire")
	    TNT1 A 0 A_Recoil(0.05)
		TNT1 A 0 A_FireCustomMissile("SpazerBolt1",-1,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBolt2",0,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBolt3",1,0,12,-2,0,0)
		TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_Light2
		SBEM B 1 BRIGHT A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
        TNT1 A 0 A_Light1
		SBEM C 1 BRIGHT A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		SBEM D 1 A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
		SBEM EEFFFEEGHA 1 A_WeaponReady(WRF_NOSWITCH)
        SBEM A 6 A_WeaponReady(WRF_NOSWITCH)
		Goto Ready
    Hold3:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",31,"HoldFinal")
        TNT1 A 0 A_GiveInventory("PowerBeamChargeLevel",1)
		SBEM AA 1 BRIGHT A_SpawnItemEx("SpazerBeamCharging2",-15,64,28,0,0,0,-270,32) 
        TNT1 A 0 A_Refire
        goto HoldFire
    Hold4:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",31,"HoldFinal")
        TNT1 A 0 A_GiveInventory("PowerBeamChargeLevel",1)
		SBEM AA 1 BRIGHT A_SpawnItemEx("SpazerBeamCharging3",-15,64,28,0,0,0,-270,32) 
        TNT1 A 0 A_Refire
        goto HoldFire
    Hold5:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",31,"HoldFinal")
        TNT1 A 0 A_GiveInventory("PowerBeamChargeLevel",1)
		SBEM AA 1 BRIGHT A_SpawnItemEx("SpazerBeamCharging4",-15,64,28,0,0,0,-270,32) 
        TNT1 A 0 A_Refire
        goto HoldFire
    Hold6:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",31,"HoldFinal")
        TNT1 A 0 A_GiveInventory("PowerBeamChargeLevel",1)
		SBEM AA 1 BRIGHT A_SpawnItemEx("SpazerBeamCharging5",-15,64,28,0,0,0,-270,32) 
        TNT1 A 0 A_Refire
        goto HoldFire
    Hold7:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("PowerBeamChargeLevel",31,"HoldFinal")
        TNT1 A 0 A_GiveInventory("PowerBeamChargeLevel",1)
		SBEM AA 1 BRIGHT A_SpawnItemEx("SpazerBeamCharging6",-15,64,28,0,0,0,-270,32) 
        TNT1 A 0 A_Refire
        goto HoldFire
    HoldFinal:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("PowerBeamCharged",1)
        TNT1 A 0 A_PlaySoundEx("pbeam/idle","soundslot7")
    HoldFinal2:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("PowerBeamIdle",26,"HoldFinal3") // The shit I do because A_StopSoundEx doesn't work online.
        TNT1 A 0 A_GiveInventory("PowerBeamIdle",1)
		SBEM AA 1 BRIGHT A_SpawnItemEx("SpazerBeamCharging7",-15,64,28,0,0,0,-270,32) 
        TNT1 A 0 A_Refire
        TNT1 A 0 A_PlaySoundEx("silence","soundslot7")//A_StopSoundEx("soundslot7")
		TNT1 A 0 A_PlayWeaponSound("sbeam/chfire")
        TNT1 A 0 A_TakeInventory("PowerBeamCharged",1)
        TNT1 A 0 A_TakeInventory("PowerBeamChargeLevel",34)
        TNT1 A 0 A_TakeInventory("PowerBeamIdle",34)
        TNT1 A 0 A_ClearRefire
	    TNT1 A 0 A_Recoil(2)
	    TNT1 A 0 A_ZoomFactor(0.95,ZOOM_INSTANT)
	    TNT1 A 0 Radius_Quake(1,8,0,1,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBolt1",-1,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBolt2",0,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBolt3",1,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBoltCharged1",0,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBoltCharged2",0,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBoltCharged3",0,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBoltCharged4",0,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBoltCharged5",-1,0,12,-2,0,0)
		TNT1 A 0 A_FireCustomMissile("SpazerBoltCharged6",1,0,12,-2,0,0)
		TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_Light2
		SBEM B 1 BRIGHT A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
        TNT1 A 0 A_Light1
	    TNT1 A 0 A_ZoomFactor(0.975,ZOOM_INSTANT)
		SBEM C 1 BRIGHT A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		TNT1 A 0 A_ZoomFactor(1.00,ZOOM_INSTANT)
		SBEM D 1 A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
		SBEM EEFFFEEGHA 1 A_WeaponReady(WRF_NOSWITCH)
        SBEM A 6 A_WeaponReady(WRF_NOSWITCH)
		Goto Ready
    HoldFinal3:
        TNT1 A 0
        TNT1 A 0 A_TakeInventory("PowerBeamIdle",99)
        TNT1 A 0 A_PlaySoundEx("pbeam/idle","soundslot7")
        goto HoldFinal2

    AltFire:
        TNT1 A 0 A_JumpIfNoAmmo("Ready")
		TNT1 A 0 A_PlayWeaponSound("missile/fire")
	    TNT1 A 0 A_Recoil(2)
	    TNT1 A 0 A_ZoomFactor(0.95,ZOOM_INSTANT)
	    TNT1 A 0 Radius_Quake(1,8,0,1,0)
		TNT1 A 0 A_FireCustomMissile("PowerMissile",0,1,12,-2,0,0)
		TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_Light2
		SBEM B 1 BRIGHT A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
        TNT1 A 0 A_Light1
	    TNT1 A 0 A_ZoomFactor(0.975,ZOOM_INSTANT)
		SBEM C 1 BRIGHT A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		TNT1 A 0 A_ZoomFactor(1.00,ZOOM_INSTANT)
		SBEM D 1 A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
		SBEM EEFFFEEGGHHAA 1 A_WeaponReady(WRF_NOSECONDARY|WRF_NOPRIMARY|WRF_NOSWITCH)
        SBEM A 8 A_WeaponReady(WRF_NOSECONDARY|WRF_NOSWITCH)
		Goto Ready
    }
}

actor SpazerBolt1
{
    Radius 6
    Height 4
    Projectile
    ReactionTime 9
    +NOGRAVITY
    Speed 60
    Damage (20)
    Scale 0.5
    RenderStyle Add
    Alpha 0.75
    DeathSound "sbeam/hit"
    Decal DoomImpScorch
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeVelocity(0,-12,0,CVF_RELATIVE)
	    SPZR DD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        TNT1 A 0 A_ChangeVelocity(0,12,0,CVF_RELATIVE)
    Spawn1:
	    TNT1 A 0
        SPZR ABCD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        loop
    Death:
        TNT1 A 0
        SPZR E 1 BRIGHT A_Scream
        SPZR FGHIJKLMNOP 1 BRIGHT 
        stop
    }
}

actor SpazerBolt2 : SpazerBolt1
{
    States
    {
    Spawn:
        TNT1 A 0
        SPZR DD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        goto Spawn1
    }
}

actor SpazerBolt3 : SpazerBolt1
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeVelocity(0,12,0,CVF_RELATIVE)
	    SPZR DD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        TNT1 A 0 A_ChangeVelocity(0,-12,0,CVF_RELATIVE)
        goto Spawn1
    }
}

actor SpazerTrail
{ 
    Alpha 0.95
    Scale 0.55
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(594,0,0,0) == 1,"Nope")
        TNT1 AA 0 A_SpawnItemEx("SpazerSpark",0,0,0,random(-100,100)/100.00,random(-100,100)/100.00,random(-100,100)/100.00,random(-180,180),32,0)//A_CustomMissile("SpazerSpark",0,0,Random(-90,90),3,Random(-45,45))
        SPZR Q 1 BRIGHT A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("SpazerSpark",0,0,0,random(-100,100)/100.00,random(-100,100)/100.00,random(-100,100)/100.00,random(-180,180),32,0)//A_CustomMissile("SpazerSpark",0,0,Random(-90,90),3,Random(-45,45))
        SPZR Q 1 BRIGHT A_FadeOut(0.1)
        TNT1 AA 0 A_SpawnItemEx("SpazerSpark",0,0,0,random(-100,100)/100.00,random(-100,100)/100.00,random(-100,100)/100.00,random(-180,180),32,0)//A_CustomMissile("SpazerSpark",0,0,Random(-90,90),3,Random(-45,45))
        SPZR R 1 BRIGHT A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("SpazerSpark",0,0,0,random(-100,100)/100.00,random(-100,100)/100.00,random(-100,100)/100.00,random(-180,180),32,0)//A_CustomMissile("SpazerSpark",0,0,Random(-90,90),3,Random(-45,45))
        SPZR R 1 BRIGHT A_FadeOut(0.1)
        loop
    Nope:
        TNT1 A 1
        stop
    }
}

actor SpazerSpark
{
    Scale 0.25
    Gravity 0.01
    Speed 2
    Renderstyle Add
    PROJECTILE
    +NOTELEPORT
    +EXPLODEONWATER
    -CANBOUNCEWATER
    +DONTSPLASH
    -SOLID
    -NOGRAVITY
    +DOOMBOUNCE
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(594,0,0,0) == 1,"Nope")
        SPTR ABCDEF 1 BRIGHT A_FadeOut(0.05)
        loop
    Nope:
        TNT1 A 1
        stop
    }
}

actor SpazerBeamCharging1
{
    RenderStyle Add
    Alpha 0.75
    Scale 0.07
    +CLIENTSIDEONLY
    +NOINTERACTION
    +NOTIMEFREEZE
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(594,0,0,0) == 1,"Nope")
        TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4")
        TNT1 A 1
        loop
    Spawn1:
        SBCH A 2 bright
        stop
    Spawn2:
        SBCH B 2 bright
        stop
    Spawn3:
        SBCH C 2 bright
        stop
    Spawn4:
        SBCH D 2 bright
        stop
    Nope:
        TNT1 A 1
        stop
    }
}

actor SpazerBeamCharging2 : SpazerBeamCharging1 { scale 0.08 }
actor SpazerBeamCharging3 : SpazerBeamCharging1 { scale 0.09 }
actor SpazerBeamCharging4 : SpazerBeamCharging1 { scale 0.105 }
actor SpazerBeamCharging5 : SpazerBeamCharging1 { scale 0.12 }
actor SpazerBeamCharging6 : SpazerBeamCharging1 { scale 0.135 }
actor SpazerBeamCharging7 : SpazerBeamCharging1 { scale 0.15 }

actor SpazerBoltCharged1 : SpazerBolt1
{
    Damage (15)
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeVelocity(0,-6,12,CVF_RELATIVE)
	    SPZR DD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        TNT1 A 0 A_ChangeVelocity(0,6,-12,CVF_RELATIVE)
        goto Spawn1
    }
}

actor SpazerBoltCharged2 : SpazerBolt1
{
    Damage (15)
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeVelocity(0,6,12,CVF_RELATIVE)
	    SPZR DD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        TNT1 A 0 A_ChangeVelocity(0,-6,-12,CVF_RELATIVE)
        goto Spawn1
    }
}

actor SpazerBoltCharged3 : SpazerBolt1
{
    Damage (15)
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeVelocity(0,-6,-12,CVF_RELATIVE)
	    SPZR DD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        TNT1 A 0 A_ChangeVelocity(0,6,12,CVF_RELATIVE)
        goto Spawn1
    }
}

actor SpazerBoltCharged4 : SpazerBolt1
{
    Damage (15)
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeVelocity(0,6,-12,CVF_RELATIVE)
	    SPZR DD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        TNT1 A 0 A_ChangeVelocity(0,-6,12,CVF_RELATIVE)
        goto Spawn1
    }
}

actor SpazerBoltCharged5 : SpazerBolt1
{
    Damage (10)
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeVelocity(0,-32,0,CVF_RELATIVE)
	    SPZR DD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        TNT1 A 0 A_ChangeVelocity(0,32,0,CVF_RELATIVE)
        goto Spawn1
    }
}

actor SpazerBoltCharged6 : SpazerBolt1
{
    Damage (10)
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeVelocity(0,32,0,CVF_RELATIVE)
	    SPZR DD 1 BRIGHT A_CustomMissile("SpazerTrail",0,0,0)
        TNT1 A 0 A_ChangeVelocity(0,-32,0,CVF_RELATIVE)
        goto Spawn1
    }
}