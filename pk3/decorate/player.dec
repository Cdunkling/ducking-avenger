actor MorphBallActivate : CustomInventory
{
    +INVBAR
    +INVENTORY.UNDROPPABLE
    +INVENTORY.HUBPOWER
    Inventory.MaxAmount 1
    States
    {
    Use:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("DisableMorph",1,"Nope")
        TNT1 A 0 ACS_ExecuteAlways(588, 0, 0, 0, 0)
        fail
    Nope:
        TNT1 A 0
        fail
    }
}

actor MorphBallDeactivate : CustomInventory
{
    +INVBAR
    +INVENTORY.UNDROPPABLE
    +INVENTORY.HUBPOWER
    Inventory.MaxAmount 1
    States
    {
      Use:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("DisableMorph",1,"Nope")
        TNT1 A 0 ACS_ExecuteAlways(588, 0, 1, 0, 0)
        fail
    Nope:
        TNT1 A 0
        fail
    }
}

actor SamusPlayer : PlayerPawn
{
    Gravity 0.65
    Player.JumpZ 9
    Player.ForwardMove 0.9, 0.9
    Player.SideMove 0.9, 0.9
    Player.SoundClass "Hunter"
    Player.DisplayName "Hunter"
    Player.StartItem "Power Beam"
    Player.StartItem "MorphBallActivate", 1
    Player.StartItem "PlayerMorphCamera", 1
    Player.StartItem "PowerBeamAcquired", 1
    Player.StartItem "MissileAmmo", 5
    Player.StartItem "BombCount", 3
    Player.StartItem "Health100", 100
    Player.StartItem "Health200", 200
    Player.StartItem "Health300", 300
    Player.StartItem "Health400", 400
    Player.StartItem "Health500", 500
    Player.StartItem "Health600", 600
    Player.StartItem "Health700", 700
    Player.StartItem "Health800", 800
    Player.StartItem "Health900", 900
    Player.StartItem "Health1000", 1000
    Player.StartItem "Health1100", 1100
    DamageFactor "NoDamage", 0.0
    DamageFactor "Drowning", 0.0
    DamageFactor "Normal", 0.75
    // Technically all this is covered by DamageFactor Normal, but I like having all the damagetypes listed in one place.
    DamageFactor "PowerBeam", 0.45
    DamageFactor "PowerCharged", 0.65
    DamageFactor "PowerCombo", 0.65
    DamageFactor "SpazerBeam", 0.45
    DamageFactor "SpazerBeamCharged", 0.5
    DamageFactor "SpazerCombo", 0.5
    DamageFactor "PlasmaBeam", 0.45
    DamageFactor "PlasmaCharged", 0.65
    DamageFactor "PlasmaCombo", 0.75
    DamageFactor "WaveBeam", 0.45
    DamageFactor "WaveCharged", 0.65
    DamageFactor "WaveCombo", 0.65
    DamageFactor "IceBeam", 0.45
    DamageFactor "IceCharged", 0.65
    DamageFactor "IceCombo", 2.00
    DamageFactor "IceBeamExplode", 0.75
    DamageFactor "IceChargedExplode", 0.75
    DamageFactor "LongBeam", 0.45
    DamageFactor "LongCharged", 0.65
    DamageFactor "LongCombo", 0.65
    DamageFactor "DoomCannon", 0.65
    DamageFactor "Missile", 0.45
    DamageFactor "SuperMissile", 0.45
    DamageFactor "BoostBall", 0.65
    DamageFactor "Bomb", 0.75
    DamageFactor "PowerBomb", 0.35
    DamageFactor "LittleMacUppercut", 3.0
    DamageFactor "SpeedBooster", 0.25
    RadiusDamageFactor 0.75
    Player.ColorRange 16,47 // Oh man, Tumblr's going to have me by the balls for this.
    +NOSKIN
    +NOBLOOD
    -THRUSPECIES
    Species "Player"
    States
    {
    Spawn:
        PLAY A -1
        Loop

    See:
        TNT1 A 0 A_JumpIfInventory("SpeedWalker",1,"SpeedSee")
        TNT1 A 0 A_JumpIfInventory("SpeedBooster",1,"SpeedSee")
        PLAY A 1
        PLAY A 0 A_CheckFloor("WalkSound")
        PLAY A 2
        goto ContinueOn

    WalkSound:
        PLAY A 2 A_PlaySoundEx("samus/step","soundslot6")
        goto ContinueOn

    ContinueOn:
        TNT1 A 0 A_JumpIfInventory("SpeedWalker",1,"SpeedContinueOn")
        TNT1 A 0 A_JumpIfInventory("SpeedBooster",1,"SpeedContinueOn")
        PLAY ABB 3
        PLAY C 1
        PLAY A 0 A_CheckFloor("WalkSound2")
        PLAY C 2
        goto ContinueOn2

    WalkSound2:
        PLAY C 2 A_PlaySoundEx("samus/step","soundslot6")
        goto ContinueOn2

    ContinueOn2:
        TNT1 A 0 A_JumpIfInventory("SpeedWalker",1,"SpeedContinueOn2")
        TNT1 A 0 A_JumpIfInventory("SpeedBooster",1,"SpeedContinueOn2")
        PLAY CDD 3
        goto Spawn

    Missile:
        PLAY E 12
        Goto Spawn

    Melee:
        PLAY F 6 BRIGHT
        Goto Missile

    Pain:
        PLAY G 4
        PLAY G 4 A_Pain
        Goto Spawn

    Pain.IceBeam:
    Pain.IceBeamExplode:
    Pain.IceChargedExplode:
    Pain.IceCombo:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("ibeam/chill","soundslot7")
        TNT1 A 0 A_SetBlend("89D5FF",0.5,175)
        TNT1 A 0 ACS_ExecuteAlways(594,0,1,0,0)
        Goto Pain

    Pain.IceCharged:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("ibeam/chill2","soundslot7")
        TNT1 A 0 A_SetBlend("White",0.5,35)
        TNT1 A 0 A_SetBlend("89D5FF",0.5,210)
        TNT1 A 0 ACS_ExecuteAlways(594,0,2,0,0)
        Goto Pain

    Death:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("EnemyDropSpawner",0,0,10,0,0,0,0,32)
        PLAY H 10 A_PlayerScream
        PLAY I 10
        PLAY J 10 A_NoBlocking
        PLAY KLM 10
        PLAY N -1
        Stop

    Ice:
    Death.Ice:
    Death.IceBeam:
    Death.IceCharged:
    Death.IceBeamExplode:
    Death.IceChargedExplode:
    Death.IceCombo:
        PLAY G 2
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("misc/freeze")
        TNT1 A 0 A_PlaySoundEx("ibeam/freeze","soundslot6")
        TNT1 A 0 A_SpawnItemEx("EnemyDropSpawner",0,0,10,0,0,0,0,32)
        TNT1 A 0 A_SpawnItemEx("SamusFrozen",0,0,0,0,0,0,0,32)
        ACLO E 1
        ACLO E -1
        Stop

    XDeath:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("EnemyDropSpawner",0,0,10,0,0,0,0,32)
        PLAY O 5 A_XScream
        PLAY P 5
        PLAY Q 5 A_NoBlocking
        PLAY RSTUV 5
        PLAY W -1
        Stop

    CoOpModeOn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        PLAY A 1
        goto Spawn


    SpeedSee:
        PLAY A 1 BRIGHT A_SpawnItemEx("SpeedBoosterDamage",64,0,0,0,0,0,0,48)
        PLAY A 0 A_CheckFloor("SpeedWalkSound")
        PLAY A 1 BRIGHT
        goto SpeedContinueOn

    SpeedWalkSound:
        PLAY A 1 BRIGHT A_PlaySoundEx("samus/step","soundslot6")
        goto SpeedContinueOn

    SpeedContinueOn:
        PLAY ABB 2 BRIGHT A_SpawnItemEx("SpeedBoosterDamage",64,0,0,0,0,0,0,48)
        PLAY C 1 BRIGHT A_SpawnItemEx("SpeedBoosterDamage",64,0,0,0,0,0,0,48)
        PLAY A 0 A_CheckFloor("SpeedWalkSound2")
        PLAY C 1 BRIGHT
        goto SpeedContinueOn2

    SpeedWalkSound2:
        PLAY C 1 BRIGHT A_PlaySoundEx("samus/step","soundslot6")
        goto SpeedContinueOn2

    SpeedContinueOn2:
        PLAY CDD 2 BRIGHT A_SpawnItemEx("SpeedBoosterDamage",64,0,0,0,0,0,0,48)
        TNT1 A 0 A_JumpIfInventory("SpeedBooster",1,"SpeedSee")
        goto Spawn
    }
}

actor SamusFrozen
{
    +SHOOTABLE
    +SOLID
    Height 56
    Radius 16
    Health 4
    Speed 0
    Mass 0x7fffffff
    Translation "Ice"
    DamageFactor "NoDamage", 0.0
    DamageFactor "Normal", 0.75
    States
    {
    Spawn:
        PLAY G 350
        PLAY G 1 A_Jump(128, "Death")
        Loop
    Death:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("misc/icebreak")
        TNT1 A 0 A_PlaySoundEx("ibeam/break","soundslot6")
        TNT1 AAAA 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(16,46),random(-2,2),random(-2,2),random(2,9),random(-180,180),160)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceTrailFlicker",random(-1,1),random(-1,1),random(16,46),random(-500,500)/100.00,random(-500,500)/100.00,random(-200,500)/100.00,random(-180,180),161)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceTrailFlicker",random(-1,1),random(-1,1),random(16,46),random(-500,500)/100.00,random(-500,500)/100.00,random(-200,500)/100.00,random(-180,180),161)
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceTrailFlicker3",random(-1,1),random(-1,1),random(16,46),random(-400,400)/100.00,random(-400,400)/100.00,random(10,500)/100.00,random(-180,180),161)
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceTrailFlicker3",random(-1,1),random(-1,1),random(16,46),random(-400,400)/100.00,random(-400,400)/100.00,random(10,500)/100.00,random(-180,180),161)
        TNT1 A 0 A_FreezeDeathChunks
        TNT1 A 0 A_NoBlocking
        TNT1 A 1
        Stop
    }
}