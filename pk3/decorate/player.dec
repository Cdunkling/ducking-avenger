actor BorphMallAcquired : Boolean {}
actor CanSpaceJump : Boolean {}

actor MorphBallActivate : CustomInventory
{
    +INVBAR
    +INVENTORY.UNDROPPABLE
    +INVENTORY.HUBPOWER
	Inventory.MaxAmount 1
    States
    {
      Use:
	    TNT1 A 0
		//TNT1 A 0 ACS_ExecuteAlways(589)
		TNT1 A 0 ACS_ExecuteAlways(588, 0, 0, 0, 0)
        fail
    }
}

actor MorphBallDeactivate : CustomInventory
{
    +INVBAR
    +INVENTORY.UNDROPPABLE
    +INVENTORY.HUBPOWER
	Inventory.MaxAmount 1
    States
    {
      Use:
	    TNT1 A 0
		//TNT1 A 0 ACS_ExecuteAlways(589)
		TNT1 A 0 ACS_ExecuteAlways(588, 0, 1, 0, 0)
        fail
    }
}

actor SamusPlayer : PlayerPawn
{
    Gravity 0.65
    Player.JumpZ 9
    Player.ForwardMove 0.9, 0.9
    Player.SideMove 0.9, 0.9
    Player.SoundClass "Hunter"
    Player.DisplayName "Hunter"
	Player.StartItem "Power Beam"
    Player.StartItem "MorphBallActivate", 1
	Player.StartItem "MissileAmmo", 5
    Player.StartItem "BombCount", 3
    DamageFactor "NoDamage", 0.0
    DamageFactor "Normal", 0.75
    RadiusDamageFactor 0.75
    Player.ColorRange 112, 127
    +NOSKIN
    States
    {
    Spawn:
        PLAY A -1
        Loop

    See:
        PLAY A 1
        PLAY A 0 A_CheckFloor("WalkSound")
        PLAY A 2
        goto ContinueOn

    WalkSound:
        PLAY A 2 A_PlaySoundEx("samus/step","soundslot6")
		goto ContinueOn

    ContinueOn:
        PLAY ABB 3
        PLAY C 1
        PLAY A 0 A_CheckFloor("WalkSound2")
        PLAY C 2
        goto ContinueOn2

    WalkSound2:
        PLAY C 2 A_PlaySoundEx("samus/step","soundslot6")
		goto ContinueOn2

    ContinueOn2:
        PLAY CDD 3
        goto Spawn

    Missile:
        PLAY E 12
        Goto Spawn

    Melee:
        PLAY F 6 BRIGHT
        Goto Missile

    Pain:
        PLAY G 4
        PLAY G 4 A_Pain
        Goto Spawn

    Death:
        PLAY H 10
        PLAY I 10 A_PlayerScream
        PLAY J 10 A_NoBlocking
        PLAY KLM 10
        PLAY N -1
        Stop

    XDeath:
        PLAY O 5
        PLAY P 5 A_XScream
        PLAY Q 5 A_NoBlocking
        PLAY RSTUV 5
        PLAY W -1
        Stop
    }
}